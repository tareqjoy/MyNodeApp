// https://github.com/crate/cratedb-flink-jobs/blob/main/build.gradle
plugins {
    id 'java'
    id 'application'
    id "idea"
    // shadow plugin to produce fat JARs
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

tasks.register("printProjectVersion") {
    println "ProjectVersion: " + project.version
}

tasks.register("printFlinkVersion") {
    println "FlinkVersion: " + flinkVersion
}


ext {
    flinkVersion = '1.20.3'
    log4jVersion = '2.25.3'
    lombokVersion = '1.18.42'
}
version = project.findProperty("appVersion") ?: "0.1.0"

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
// declare where to find the dependencies of your project
repositories {
    mavenCentral()
}
// NOTE: We cannot use "compileOnly" or "shadow" configurations since then we could not run code
// in the IDE or with "gradle run". We also cannot exclude transitive dependencies from the
// shadowJar yet (see https://github.com/johnrengelman/shadow/issues/159).
// -> Explicitly define the // libraries we want to be included in the "flinkShadowJar" configuration!
configurations {
    flinkShadowJar // dependencies which go into the shadowJar
    // always exclude these (also from transitive dependencies) since they are provided by Flink
    flinkShadowJar.exclude group: 'org.apache.flink', module: 'force-shading'
    flinkShadowJar.exclude group: 'com.google.code.findbugs', module: 'jsr305'
    flinkShadowJar.exclude group: 'org.slf4j'
    flinkShadowJar.exclude group: 'org.apache.logging.log4j'
}
// declare the dependencies for your production and test code
dependencies {
    // --------------------------------------------------------------
    // Compile-time dependencies that should NOT be part of the
    // shadow (uber) jar and are provided in the lib folder of Flink
    // --------------------------------------------------------------
    implementation "org.apache.flink:flink-streaming-java:${flinkVersion}"
    implementation "org.apache.flink:flink-clients:${flinkVersion}"
    // --------------------------------------------------------------
    // Dependencies that should be part of the shadow jar, e.g.
    // connectors. These must be in the flinkShadowJar configuration!
    // --------------------------------------------------------------
    //flinkShadowJar "org.apache.flink:flink-connector-kafka:${flinkVersion}"
    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
    runtimeOnly "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    runtimeOnly "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    // Add test dependencies here.
    // testCompile "junit:junit:4.12"

    flinkShadowJar "org.apache.flink:flink-connector-kafka:3.4.0-1.20"
    implementation "com.googlecode.json-simple:json-simple:1.1.1"
    implementation "org.apache.flink:flink-connector-base:${flinkVersion}"
    implementation "org.apache.flink:flink-core:${flinkVersion}"
    implementation "org.apache.flink:flink-java:${flinkVersion}"
    flinkShadowJar "redis.clients:jedis:7.2.1"

    runtimeOnly "org.apache.flink:flink-runtime:${flinkVersion}"

    // lombok
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}


application {
    mainClassName = "analytics.App"
    applicationDefaultJvmArgs = ["-Dlog4j.configurationFile=log4j2.properties"]
}

// make compileOnly dependencies available for tests:
sourceSets {
    main.compileClasspath += configurations.flinkShadowJar
    main.runtimeClasspath += configurations.flinkShadowJar
    test.compileClasspath += configurations.flinkShadowJar
    test.runtimeClasspath += configurations.flinkShadowJar
    javadoc.classpath += configurations.flinkShadowJar
}
run.classpath = sourceSets.main.runtimeClasspath

jar {
    manifest {
        attributes 'Built-By': System.getProperty('user.name'),
                'Build-Jdk': System.getProperty('java.version')
    }
    archiveBaseName.set('flink-jobs')
    archiveVersion.set(project.version.toString())
    archiveClassifier.set('thin')
}

shadowJar {
    configurations = [project.configurations.flinkShadowJar]
    archiveBaseName.set('flink-jobs')
    archiveVersion.set(project.version.toString())
    archiveClassifier.set('fat')
}

assemble.dependsOn(shadowJar)

tasks.register('runJob', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = "analytics.App"
}
